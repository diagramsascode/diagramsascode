subprojects{
	group = 'org.diagramsascode'
	version = '0.1'
	
	apply plugin: 'java-library'
	apply plugin: 'maven-publish'
	apply plugin: 'eclipse'
	apply plugin: "jacoco"
	
	compileJava {
	  sourceCompatibility = '1.8'
	  targetCompatibility = '1.8'
	}
	
	repositories {
		mavenCentral()
		maven {
            url "https://oss.sonatype.org/content/repositories/snapshots/"
        }
	}
	
	dependencies {
		testImplementation("org.junit.jupiter:junit-jupiter-api:5.7.1")
		testRuntimeOnly("org.junit.jupiter:junit-jupiter-engine:5.7.1")
	}

	test {
		useJUnitPlatform()
	}
	
	if (project.hasProperty("signing.keyId")) {
		apply plugin: 'signing'
	
		publishing {
			publications {
				mavenJava(MavenPublication) {
					pom{
						name = 'diagrams as code'
						description = 'Enables you to generate diagrams from your code.'
						url = 'http://diagramsascode.org/'
						
						from components.java
						
						licenses {
							license {
								name = 'The Apache Software License, Version 2.0'
								url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
							}
						}
						
						developers {
							developer {
								id = 'bertilmuth'
								name = 'Bertil Muth'
							}
						}
						
						scm {
							url = 'scm:git@github.com:diagramsascode/diagramsascode.git'
							connection = 'scm:git@github.com:diagramsascode/diagramsascode.git'
							developerConnection = 'scm:git@github.com:diagramsascode/diagramsascode.git'
						}
					}
				}
			}
			
			repositories {
				maven {
					credentials {
						username sonatypeUsername
						password sonatypePassword
					}
					
					def releasesRepoUrl = "https://oss.sonatype.org/service/local/staging/deploy/maven2/"
					def snapshotsRepoUrl = "https://oss.sonatype.org/content/repositories/snapshots/"
					url = version.endsWith('SNAPSHOT') ? snapshotsRepoUrl : releasesRepoUrl
				}
			}
		}
		
		signing {
			sign publishing.publications.mavenJava
		}
	}
	
	task javadocJar(type: Jar) {
    	classifier = 'javadoc'
    	from javadoc
	}
	
	task sourcesJar(type: Jar) {
    	classifier = 'sources'
    	from sourceSets.main.allSource
	}

	artifacts {
	    archives javadocJar, sourcesJar
	}
}
